apply plugin: 'com.android.application'

static def gitLatestTag() {
    try {
        def cmd = 'git describe --abbrev=0 --tags'
        def result = cmd.execute().text.trim()
        return result
    } catch (Exception e) {
        return '1.0.0'
    }
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId "com.shiqkuangsan.fragmentdemo"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 1
        versionName gitLatestTag()
    }

    buildTypes {
        release {
            minifyEnabled false

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == 'release') {
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    def fileName = "fragmentation.apk"
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile rootProject.ext.dependencies.appcompatv7
    compile rootProject.ext.dependencies.design
    compile rootProject.ext.dependencies.cardview
    compile rootProject.ext.dependencies.recyclerview

    // 如果想使用SwipeBack 滑动边缘退出Fragment/Activity功能，完整的添加规则如下：
    compile 'me.yokeyword:fragmentation:1.1.4'
// swipeback基于fragmentation, 如果是自定制SupportActivity/Fragment，则参照SwipeBackActivity/Fragment实现即可
    compile 'me.yokeyword:fragmentation-swipeback:1.1.4'

    // EventBus
    compile 'org.greenrobot:eventbus:3.0.0'
//    apt 'org.greenrobot:eventbus-annotation-processor:3.0.1'
}

